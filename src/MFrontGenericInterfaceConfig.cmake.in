@PACKAGE_INIT@

set(MGIS_FUNCTION_SUPPORT @enable-mgis-function@)
set(MGIS_HAVE_TFEL @MGIS_HAVE_TFEL@)

if(MGIS_HAVE_TFEL)

   set(_TFEL_MODULE_PATH "@TFEL_DIR@")

   if(TFEL_DIR)
     set(_TFEL_MODULE_PATH "${TFEL_DIR}")
   elseif(DEFINED ENV{${TFEL_DIR}})
     set(_TFEL_MODULE_PATH "$ENV{TFEL_DIR}")
   else(TFEL_DIR)
     if(TFELHOME)
       set(_TFEL_MODULE_PATH "${TFELHOME}")
     else(TFELHOME)
       if(DEFINED ENV{${VERSIONED_TFEL_HOME}})
          set(_TFEL_MODULE_PATH "$ENV{${VERSIONED_TFEL_HOME}}/share/tfel/cmake")
       else(DEFINED ENV{${VERSIONED_TFEL_HOME}})
         if(DEFINED ENV{TFELHOME})
            set(_TFEL_MODULE_PATH "$ENV{TFELHOME}/share/tfel/cmake")      
         endif(DEFINED ENV{TFELHOME})
       endif(DEFINED ENV{${VERSIONED_TFEL_HOME}})
     endif(TFELHOME)
   endif(TFEL_DIR)

  find_package(TFELMath REQUIRED
    HINTS "${CMAKE_CURRENT_LIST_DIR}" "${_TFEL_MODULE_PATH}")
  find_package(TFELMFrontDatabase REQUIRED
    HINTS "${CMAKE_CURRENT_LIST_DIR}" "${_TFEL_MODULE_PATH}")

endif(MGIS_HAVE_TFEL)

set(MGIS_Threads_FOUND @Threads_FOUND@)
if(MGIS_Threads_FOUND)
  find_package(Threads REQUIRED)
endif(MGIS_Threads_FOUND)

set(MGIS_HDF5_SUPPORT @MGIS_HDF5_SUPPORT@)
if(MGIS_HDF5_SUPPORT)
  set(MGIS_HDF5_DIR "@HDF5_DIR@")
  if(MGIS_HDF5_DIR)
    set(_HDF5_MODULE_PATH "@HDF5_DIR@")
    if(HDF5_DIR)
      set(_HDF5_MODULE_PATH "${HDF5_DIR}")
    elseif(DEFINED ENV{${HDF5_DIR}})
      set(_HDF5_MODULE_PATH "$ENV{HDF5_DIR}")
    endif(HDF5_DIR)
    find_package(HDF5 REQUIRED COMPONENTS CXX
                 HINTS "${CMAKE_CURRENT_LIST_DIR}" "${_HDF5_MODULE_PATH}")
  else()
    find_package(HDF5 REQUIRED COMPONENTS CXX)
  endif()
endif(MGIS_HDF5_SUPPORT)

set(MGIS_REQUIRED_ADDITIONAL_PACKAGES "@MGIS_REQUIRED_ADDITIONAL_PACKAGES@")
foreach(_package ${MGIS_REQUIRED_ADDITIONAL_PACKAGES})
  find_package(${_package} REQUIRED)
endforeach()

include("${CMAKE_CURRENT_LIST_DIR}/MFrontGenericInterfaceTargets.cmake")
